# Maintainer: Tim HÃ¼tz <tim@huetz.biz>

# Forked from pommed. Previous header:
# Maintainer: Corrado Primier <ilbardo gmail com>
# Contributor: Other contributors existed but lost information
# Contributor: dpevp <daniel.plaza.espi@gmail.com>
# Contributor: jordi Cerdan (jcerdan) <jcerdan@tecob.com>

pkgname=pommed-ng
pkgver=1.0.2
pkgrel=1
pkgdesc="Handles the hotkeys of Apple MacBook (Pro) laptops"
arch=('i686' 'x86_64')
url="https://github.com/thuetz/NGPommed"
license=('GPL2')
makedepends=('pkgconfig' 'cmake' 'dbus')
depends=('confuse' 'audiofile' 'pciutils')
conflicts=('pommed' 'pommed-light' 'pommed-git' 'pommed-jalaziz' 'gpomme')
optdepends=('eject: disc ejection support')
source=( "https://github.com/thuetz/NGPommed/archive/v${pkgver}.tar.gz" )
dfname="NGPommed-${pkgver}"
sha256sums=('86a5128ea9adf3ed9758cb76121b46ad89566fcfec32eb3ddc68f06b867014bb')

prepare() {
  [[ -d build ]] && rm -rf build
  mkdir build
}

build() {
  cd build
	cmake ${srcdir}/${dfname} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
  #install -Dm755 ${srcdir}/${dfname}/source/pommed ${pkgdir}/usr/bin/pommed

  install -Dm644 ${srcdir}/${dfname}/data/pommed.conf.mactel ${pkgdir}/etc/pommed.conf.mactel
  install -Dm644 ${srcdir}/${dfname}/data/pommed.conf.pmac ${pkgdir}/etc/pommed.conf.pmac

  # man page
  install -Dm644 ${srcdir}/${dfname}/data/pommed.1 ${pkgdir}/usr/share/man/man1/pommed.1

  # sounds
  install -Dm644 ${srcdir}/${dfname}/data/goutte.wav ${pkgdir}/usr/share/pommed/goutte.wav
  install -Dm644 ${srcdir}/${dfname}/data/click.wav ${pkgdir}/usr/share/pommed/click.wav

  # systemd
  install -Dm644 ${srcdir}/${dfname}/data/pommed.service ${pkgdir}/usr/lib/systemd/system/pommed.service
}

# vim:set ts=2 sw=2 et:
